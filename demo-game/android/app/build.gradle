plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.jetbrains.kotlin.android)
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def sdl3Dir = properties.getProperty('sdl3.dir')
if (sdl3Dir == null) {
    throw new IllegalArgumentException('no [sdl3.dir] property in [local.properties] file')
}
def sdl3ttfDir = properties.getProperty('sdl3ttf.dir')
if (sdl3ttfDir == null) {
    throw new IllegalArgumentException('no [sdl3ttf.dir] property in [local.properties] file')
}

android {
    namespace 'com.kostahka.kenginedemogame'
    compileSdk 34

    defaultConfig {
        applicationId "com.kostahka.kenginedemogame"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-24', '-DANDROID_STL=c++_static',
                        "-DSDL3_SRC_DIR=$sdl3Dir", "-DSDL3_TTF_SRC_DIR=$sdl3ttfDir",
                        "-DKENGINE_DEMO_GAME=ON"
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.1'
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    sourceSets.main {
        jniLibs.srcDir 'libs'
    }
    externalNativeBuild {
        cmake {
            path '../../../CMakeLists.txt'
        }
    }
}

tasks.register("copyDemoGameAssets", Copy)
{
    from(file('../../assets'))
    into('src/main/assets')
}

preBuild.dependsOn copyDemoGameAssets

dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    implementation project(':kengine')
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.ui.test.junit4
    debugImplementation libs.androidx.ui.tooling
    debugImplementation libs.androidx.ui.test.manifest
}